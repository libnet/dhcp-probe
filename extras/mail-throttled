#!/usr/local/bin/perl5 -w

# $Header: /usr/local/etc/RCS/mail-throttled,v 1.5 2003/05/20 03:15:42 root Exp $

# mail-throttled [-D dbm_file] -k key -t throttle_seconds [-f from] -r recipient [-d] [-s subject]
#
# Sends mail body (read from STDIN) to 'recipient', but avoids doing so "too frequently."
#
# You provide a 'key', which is an arbitrary string used to identify this notification.
# You also provide 'throttle_seconds', an integer.  If we've sent anything that
# specified this 'key' within the last 'throttle_seconds', we do not send the message.
# Otherwise, we send the message, and the remember that we've sent a message for this 'key'
# at the current time.
#
# This key/timesent tuples are stored on-disk, in a dbm.  As a result, the 'key'
# you supply must satisfy the syntactic requirements for dbm keys.  We never
# clean this dbm.  You can safely erase it entirely, if you don't mind losing
# the state.
#
# The 'recipient' should be a valid email address.  Naturally, it should not
# be one that will cause any ack or bounce mail to return to us!
# If there are several addresses (delimited by spaces), be sure to quote them as a single arg.
#
# If a subject is specified, be sure to quote it if it contains any spaces or other shell
# metachars.
#
# Irwin Tillman

use Getopt::Std;
use strict;

use vars qw($DBM_FILE_DEFAULT $MAILCMD $MAILCMD_OPTS $FROM_DEFAULT);
$DBM_FILE_DEFAULT = '/usr/local/lib/mail-throttled';
$MAILCMD = "/usr/lib/sendmail";
# $MAILCMD_OPTS = "-t -ODeliveryMode=queueonly";
$MAILCMD_OPTS = "-t"; 
$FROM_DEFAULT = "root";

(my $prog = $0) =~ s/.*\///;

use vars qw($opt_f $opt_D $opt_d $opt_k $opt_r $opt_t $opt_s); # placate use strict
&getopts('dD:f:k:r:s:t:');

my $debug = $opt_d || "";
my $dbm_file = $opt_D || $DBM_FILE_DEFAULT;
my $key = $opt_k || "";
my $from = $opt_f || $FROM_DEFAULT;
my $recipient = $opt_r || "";
my $throttle_secs = $opt_t || 1;
my $subject = $opt_s || "";
print STDERR "${prog}:\nkey=$key\nthrottle_secs=$throttle_secs\nfrom=$from\nrecipient=$recipient\nsubject=$subject\n" if $debug;

&Usage() unless ($key && $throttle_secs && $recipient);

my %last_sent = ();
dbmopen(%last_sent, $dbm_file, 0644) or die "${prog}: dbmopen(${dbm_file}): $!\n";

my @mailbody = "";
@mailbody = <STDIN>; # read it even if we decide not to send it

my $now = time;

print STDERR "now = $now\n" if $debug;

$last_sent{$key} = 0 unless defined($last_sent{$key}); # so its defined before we use it in subtraction (placate use strict)

if ($now - $last_sent{$key} >= $throttle_secs) {
	print STDERR "last_sent = $last_sent{$key}, will send\n" if $debug;
	open(MAIL, "| $MAILCMD $MAILCMD_OPTS -f\"$from\"") or die "${prog}: can't execute '${MAILCMD}': $!\n";
	print MAIL	"From: $from\n",
				"To: $recipient\n",
				($subject ? "Subject: $subject\n" : "") ,
				"\n",
				@mailbody;
	close(MAIL) or die "${prog}: can't execute '${MAILCMD}': $!\n";

	$last_sent{$key} = $now;
} else {
	print STDERR "last_sent = $last_sent{$key}, suppressing\n" if $debug;
}

dbmclose %last_sent;

exit 0;



sub Usage {
	print STDERR "Usage: $prog [-D dbm_file] -k key -t throttle_seconds [-f from] -r recipient [-s subject]\n";
	exit 1;
}
